# Profile a dataset by counting combinations of:
# - predicate
# - object class (if resource)
# - object datatype (if literal)
# - object language (if langString)
# There is a limit on the size of the dataset that you can profile this way.
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>

SELECT (COUNT(*) as ?c) ?pred ?objType ?objDatatype ?objLanguage WHERE {
  {
    SELECT ?sub ?pred ?objType ?objDatatype ?objLanguage WHERE {
      ?sub ?pred ?obj.
      #FILTER (?pred != rdf:type)
      optional { ?obj a ?objType. }
  
      BIND(datatype(?obj) as ?objDatatype)
      BIND(lang(?obj) as ?objLanguage)
    }
    ORDER BY ?sub
    LIMIT 10000
  }
}
GROUP BY ?pred ?objType ?objDatatype ?objLanguage
ORDER BY desc(?c)
LIMIT 100